/**
 * GaussianBlur.cgfx
 * (cc) Callum Hay, 2013
 *
 * Performs a gaussian blur of varying kernel size and sigma on a given scene texture.
 */

#define PI 3.14159265358979323846264338327950288

// This should usually be equal to
// 1.0f / texture_pixel_width for a horizontal blur.
float BlurSizeHorizontal;

// This should usually be equal to
// 1.0f / texture_pixel_height for a vertical blur.
float BlurSizeVertical;

// The sigma value for the gaussian function: higher value means more blur
// A good value for 9x9 is around 3 to 5
// A good value for 7x7 is around 2.5 to 4
// A good value for 5x5 is around 2 to 3.5
float Sigma;            

texture SceneTex <
    string ResourceName = "lasers.jpg";
    string UIName = "Scene Texture";
    string ResourceType = "2D";
>;

sampler2D SceneSampler = sampler_state {
    Texture = <SceneTex>;   
    WrapS = ClampToEdge;
    WrapT = ClampToEdge;
};

float4 performGaussianBlur(float2 UV, float blurPixelsPerSide, float2 blurMultiplyVec, float blurSize) {
	
    // Incremental Gaussian Coefficent Calculation (See GPU Gems 3 pp. 877 - 889)
    float3 incrementalGaussian;
    incrementalGaussian.x = 1.0 / (sqrt(2.0 * PI) * Sigma);
    incrementalGaussian.y = exp(-0.5 / (Sigma * Sigma));
    incrementalGaussian.z = incrementalGaussian.y * incrementalGaussian.y;

    float4 avgValue = float4(0.0f, 0.0f, 0.0f, 0.0f);
    float coefficientSum = 0.0f;

    // Take the central sample first...
    avgValue += tex2D(SceneSampler, UV) * incrementalGaussian.x;
    coefficientSum += incrementalGaussian.x;
    incrementalGaussian.xy *= incrementalGaussian.yz;

    // Go through the remaining samples
    for (float i = 1.0f; i <= blurPixelsPerSide; i++) {
        avgValue += tex2D(SceneSampler, UV - i * blurSize * blurMultiplyVec) * incrementalGaussian.x;
        avgValue += tex2D(SceneSampler, UV + i * blurSize * blurMultiplyVec) * incrementalGaussian.x;
        coefficientSum += 2 * incrementalGaussian.x;
        incrementalGaussian.xy *= incrementalGaussian.yz;
    }
    
    return (avgValue / coefficientSum);
}


float4 gauss3x3_rows_PS(float2 UV : TEXCOORD0) : COLOR {
	return performGaussianBlur(UV, 1.0, float2(1, 0), BlurSizeHorizontal);
}
float4 gauss3x3_cols_PS(float2 UV : TEXCOORD0) : COLOR {
	return performGaussianBlur(UV, 1.0, float2(0, 1), BlurSizeVertical);
}

float4 gauss5x5_rows_PS(float2 UV : TEXCOORD0) : COLOR {
	return performGaussianBlur(UV, 2.0, float2(1, 0), BlurSizeHorizontal);
}
float4 gauss5x5_cols_PS(float2 UV : TEXCOORD0) : COLOR {
	return performGaussianBlur(UV, 2.0, float2(0, 1), BlurSizeVertical);
}

float4 gauss7x7_rows_PS(float2 UV : TEXCOORD0) : COLOR {
	return performGaussianBlur(UV, 3.0, float2(1, 0), BlurSizeHorizontal);
}
float4 gauss7x7_cols_PS(float2 UV : TEXCOORD0) : COLOR {
	return performGaussianBlur(UV, 3.0, float2(0, 1), BlurSizeVertical);
}

technique GaussianBlur3x3 {
    pass rows {
        FragmentProgram = compile arbfp1 gauss3x3_rows_PS();
    }
    pass cols {
        FragmentProgram = compile arbfp1 gauss3x3_cols_PS();        
    }
}

technique GaussianBlur5x5 {
    pass rows {
        FragmentProgram = compile arbfp1 gauss5x5_rows_PS();
    }
    pass cols {
        FragmentProgram = compile arbfp1 gauss5x5_cols_PS();  
    }
}

technique GaussianBlur7x7 {
    pass rows {
        FragmentProgram = compile arbfp1 gauss7x7_rows_PS();
    }
    pass cols {
        FragmentProgram = compile arbfp1 gauss7x7_cols_PS();  
    }
}
