/**
 * Bloom.cgfx
 * (cc) Callum Hay, 2013
 *
 * Apply a multi-pass luminance selection and blur to a fullscreen rendering.
 */


float HighlightThreshold <
    string UIName = "Highlight Threshold";
    string UIWidget = "slider";
    float UIMin = 0.0f;
    float UIMax = 1.0f;
    float UIStep = 0.1f;
> = 0.9f;

float SceneIntensity <
    string UIName = "Scene Intensity";
    string UIWidget = "slider";
    float UIMin = 0.0f;
    float UIMax = 2.0f;
    float UIStep = 0.1f;
> = 0.5f;

float GlowIntensity <
    string UIName = "Glow Intensity";
    string UIWidget = "slider";
    float UIMin = 0.0f;
    float UIMax = 2.0f;
    float UIStep = 0.1f;
> = 0.5f;

float HighlightIntensity <
    string UIName = "Highlight intensity";
    string UIWidget = "slider";
    float UIMin = 0.0f;
    float UIMax = 10.0f;
    float UIStep = 0.1f;
> = 0.5f;

float SceneWidth <
    string UIWidget = "slider";
    float UIMin = 1;
    float UIMax = 1680;
    float UIStep = 1;
    string UIName =  "Width";
> = 1024.0f;

float SceneHeight <
    string UIWidget = "slider";
    float UIMin = 1;
    float UIMax = 1050;
    float UIStep = 1;
    string UIName =  "Height";
> = 768.0f;

texture SceneTex <
    string ResourceName;
    string UIName = "Scene Texture";
    string ResourceType = "2D";
>;
sampler2D SceneSampler = sampler_state {
    Texture = <SceneTex>;   
    WrapS = ClampToEdge;
    WrapT = ClampToEdge;
};

texture BrightBlurTex <
    string ResourceName;
    string UIName = "Scene Texture";
    string ResourceType = "2D";
>;
sampler2D BrightBlurSampler = sampler_state {
    Texture = <BrightBlurTex>;   
    WrapS = ClampToEdge;
    WrapT = ClampToEdge;
};

sampler2D BrightBlurSamplerMip3 = sampler_state {
    Texture = <BrightBlurTex>;   
    WrapS = ClampToEdge;
    WrapT = ClampToEdge;
	MinMipLevel = 3;
    MaxMipLevel = 3;
};
sampler2D BrightBlurSamplerMip4 = sampler_state {
    Texture = <BrightBlurTex>;   
    WrapS = ClampToEdge;
    WrapT = ClampToEdge;
	MinMipLevel = 4;
    MaxMipLevel = 4;
};

half luminance(half3 c) {
    return dot(c, float3(0.3f, 0.59f, 0.11f));
}

// this function should be baked into a texture lookup for performance
half filter_highlights(half3 c, uniform float threshold) {
    return smoothstep(threshold, 1.0f, luminance(c.rgb));
}

half4 bloomfilter_PS(float2 UV : TEXCOORD0) : COLOR {
    half3 sampleColour = tex2D(SceneSampler, UV).rgb;
    return half4(sampleColour, filter_highlights(sampleColour, HighlightThreshold));
}

float4 composite_bloom_PS(float2 UV : TEXCOORD0) : COLOR {   
    float4 origScene = tex2D(SceneSampler, UV);
    float4 blurTotal = clamp(tex2D(BrightBlurSampler, UV) + 
		tex2D(BrightBlurSamplerMip3, UV) + tex2D(BrightBlurSamplerMip4, UV), 0, 1);
		
    return float4(SceneIntensity*origScene.rgb + GlowIntensity*blurTotal.rgb + 
		HighlightIntensity*blurTotal.aaa, 1);
}  

technique BloomFilter {
    pass filter {
        BlendEnable = true;
        BlendFunc = int2(SrcAlpha, OneMinusSrcAlpha); 
        FragmentProgram = compile arbfp1 bloomfilter_PS();
    }
}

technique CompositeBloom {
    pass bloomComposite {
        BlendEnable = true;
        BlendFunc = int2(SrcAlpha, OneMinusSrcAlpha); 
        FragmentProgram = compile arbfp1 composite_bloom_PS();  
    }
}