/**
 * PostFirey.cgfx
 * This shader is a fullscreen post-effect used to give the impression
 * that the player is in a hot firey place. Specifically this is
 * used when in ball camera mode while the fire ball is active.
 */

float Timer : TIME <
    string UIWidget = "slider";
    float UIMin = 0.0;
    float UIMax = 4.0;
    float UIStep = 0.01;
    string UIName =  "Timer";
> = 0;

float Scale <
    string UIWidget = "slider";
    float UIMin = 0.0;
    float UIMax = 4.0;
    float UIStep = 0.1;
    string UIName =  "Scale";
> = 1;

float Freq <
    string UIWidget = "slider";
    float UIMin = 0.0;
    float UIMax = 10.0;
    float UIStep = 0.1;
    string UIName =  "Frequency";
> = 1;

float Fade <
	string UIWidget = "slider";
	string UIName =  "Fade";
	float UIMin = 0.0;
	float UIMax = 1.0;
	float UIStep = 0.001;
> = 0.0f;

//////// COLOR & TEXTURE /////////////////////

texture NoiseTexture  <
    string ResourceName = "noiseL8_32x32x32.dds";
    string UIName =  "Noise Texture";
    string ResourceType = "3D";
>;

sampler3D NoiseSampler = sampler_state {
    Texture = <NoiseTexture>;
    MinFilter = LinearMipMapLinear;
    MagFilter = Linear;
    WrapS = Repeat;
    WrapT = Repeat;
    WrapR = Repeat;
    
};  

texture SceneTexture <
    string ResourceName = "scene_background_test.jpg";
    string UIName =  "Scene Texture";
    string ResourceType = "2D";
>;

sampler2D SceneSampler = sampler_state {
    Texture = <SceneTexture>;
    WrapS = ClampToEdge;
    WrapT = ClampToEdge;
    MinFilter = LinearMipMapLinear;
    MagFilter = Linear;
};


///////// PIXEL SHADING //////////////////////

float4 post_firey_PS(float2 UV : TEXCOORD0) : COLOR {
    float3 sceneColour = tex2D(SceneSampler, UV).rgb;
    float4 texVal = tex3D(NoiseSampler, Scale * float3(UV, Freq * Timer));
    float totalTexVal = 8.0 * (abs(texVal.x - 0.25)  + abs(texVal.y - 0.125) + 
                               abs(texVal.z - 0.0625) + abs(texVal.w - 0.03125));

    float3 fireColour = Fade * lerp(float3(1.0f, 0.15f, 0.0f), 
									float3(1.0f, 0.9f, 0.0f), 
									0.5f * totalTexVal);             
    return float4(fireColour.xyz + sceneColour, 1);
}

///// TECHNIQUES /////////////////////////////

technique PostFirey < string Script = "Pass=p0;"; > {
    pass p0 < string Script = "Draw=geometry;"; > {
        FragmentProgram = compile arbfp1 post_firey_PS();
    }
}

/////////////////////////////////////// eof //
