/**
 * PostRefract.cgfx
 * Author: Callum Hay
 * The following is a post-processing effect that will take a given
 * geometry and, using its normals, distort the given rendered scene 
 * quad in the area where that mesh would normally be rendered -
 * the effect of this is a kind of 'cloaking/invisiblity' of the mesh.
 */

float Script : STANDARDSGLOBAL <
    string UIWidget = "none";
    string ScriptClass = "object";
    string ScriptOrder = "standard";
    string ScriptOutput = "color";
    string Script = "Technique=Basic;";
> = 0.8;

//// UN-TWEAKABLES - AUTOMATICALLY-TRACKED TRANSFORMS ////////////////

float4x4 WorldITXf : WorldInverseTranspose < string UIWidget="None"; >;
float4x4 WvpXf     : WorldViewProjection < string UIWidget="None"; >;
float4x4 WorldXf   : World < string UIWidget="None"; >;
float4x4 ViewIXf   : ViewInverse < string UIWidget="None"; >;

//// TWEAKABLE PARAMETERS ////////////////////
float WarpAmount <
    string UIWidget = "slider";
    float UIMin = 1;
    float UIMax = 1000;
    float UIStep = 1;
    string UIName =  "Warp Amount";
> = 1.0f;

float SceneWidth <
    string UIWidget = "slider";
    float UIMin = 1;
    float UIMax = 1680;
    float UIStep = 1;
    string UIName =  "Width";
> = 1083.0f;

float SceneHeight <
    string UIWidget = "slider";
    float UIMin = 1;
    float UIMax = 1050;
    float UIStep = 1;
    string UIName =  "Height";
> = 852.0f;

float IndexOfRefraction <
    string UIWidget = "slider";
    float UIMin = 0.0;
    float UIMax = 3.0;
    float UIStep = 0.01;
    string UIName =  "Index of Refraction";  
> = 1.33f;

// The colour of the material
float3 InvisiColour <
    string UIName =  "Invisi Colour";
    string UIWidget = "Color";
> = {1.00f, 1.00f, 1.00f};

float Timer : TIME <
    string UIWidget = "slider";
    float UIMin = 0.0;
    float UIMax = 4.0;
    float UIStep = 0.01;
    string UIName =  "Timer";
> = 0;

float Scale <
    string UIWidget = "slider";
    float UIMin = 0.0;
    float UIMax = 4.0;
    float UIStep = 0.1;
    string UIName =  "Scale";
> = 1;

float Freq <
    string UIWidget = "slider";
    float UIMin = 0.0;
    float UIMax = 10.0;
    float UIStep = 0.1;
    string UIName =  "Frequency";
> = 1;

float3 FlowDir <
    string UIName = "Flow Direction";
> = {0, 1, 1};

//////// TEXTURE /////////////////////

texture SceneTexture <
    string ResourceName = "scene_background_test.jpg";
    string UIName =  "Scene Texture";
    string ResourceType = "2D";
>;

sampler2D SceneSampler = sampler_state {
    Texture = <SceneTexture>;
    WrapS = ClampToEdge;
    WrapT = ClampToEdge;
    MinFilter = LinearMipMapLinear;
    MagFilter = Linear;
};   

texture NoiseTexture  <
    string ResourceName = "noiseL8_32x32x32.dds";
    string UIName =  "Noise Texture";
    string ResourceType = "3D";
>;

sampler3D NoiseSampler = sampler_state {
    Texture = <NoiseTexture>;
    MinFilter = LinearMipMapLinear;
    MagFilter = Linear;
    WrapS = Repeat;
    WrapT = Repeat;
    WrapR = Repeat;
    
};  

texture MaskTexture <
    string ResourceName = "Circle_Gradient_64x64.png";
    string UIName = "Mask Texture";
    string ResourceType = "2D";
>;

sampler2D MaskSampler = sampler_state {
    Texture = <MaskTexture>;
    MinFilter = LinearMipMapLinear;
    MagFilter = Linear;
    WrapS = Repeat;
    WrapT = Repeat;
};

//////// CONNECTOR DATA STRUCTURES ///////////

/* data from application vertex buffer */
struct appdata {
    float3 Position	: POSITION;
    float4 UV		    : TEXCOORD0;
    float4 Normal	  : NORMAL;
};

/* data passed from vertex shader to pixel shader */
struct vertexOutput {
    float4 HPosition    : POSITION;
    float3 WorldNormal  : TEXCOORD0;
    float3 ProjPos      : TEXCOORD1;
    float3 WorldView    : TEXCOORD2;
    float3 LocalPos     : TEXCOORD3;
    float2 UV           : TEXCOORD4;
};
 
///////// VERTEX SHADING /////////////////////

vertexOutput PostRefract_VS(appdata IN) {
    vertexOutput OUT = (vertexOutput)0;
    
    float4 Po = float4(IN.Position.xyz,1);
    float3 Pw = mul(WorldXf,Po).xyz;
    
    float3 worldNormal = mul(WorldITXf,IN.Normal).xyz;
    float3 viewToVert  = Pw - float3(ViewIXf[0].w,ViewIXf[1].w,ViewIXf[2].w);
    
    OUT.WorldNormal = normalize(worldNormal);
    OUT.WorldView   = normalize(viewToVert);
    
    float4 outPos  = mul(WvpXf,Po);
    OUT.HPosition  = outPos;
    OUT.LocalPos   = Po.xyz;
    OUT.ProjPos    = outPos.xyz / outPos.w;   
    OUT.UV         = IN.UV.xy;
    return OUT;
}

///////// PIXEL SHADING //////////////////////

/**
 * Refract the given background texture - this will use a given world view and
 * normal vector (both normalized) and find an offset for the lookup into the bg texture.
 * Returns: (x,y) offset into the bg texture to mimic the set refraction.
 */
float2 GetRefractBGOffset(float3 nWorldView, float3 nWorldNormal) {
    // This is a hacked version of the Fresnel effect - it has fixed constants
    // of 0.5 for the bias, 1 for the Scale and 1 for the power
    // The reasoning is that I want a nicely blended bit of both reflection and refraction
    // especially in cases of total internal refraction (which looks ugly and abrupt without this)
    float fresnelCoeff = clamp(1.5f + dot(nWorldView,nWorldNormal), 0, 1);
    // Find the world space reflection vector
    float3 refractionVec = refract(nWorldView, nWorldNormal, IndexOfRefraction);
    // Find the world space refraction vector (IndexOfRefraction_Medium to air)
    float3 reflectionVec = reflect(nWorldView, nWorldNormal);
    
    // Obtain the lookup vector which will be scaled/warped to get an offset for looking up
    // the background/scene texture
    float2 lookupVec = fresnelCoeff*reflectionVec.xy + (1-fresnelCoeff)*refractionVec.xy;
    
    // Use the refraction and scale it to the screen size to get an offset for looking
    // up the texel in the scene texture
    return WarpAmount * lookupVec.xy * float2(1.0/SceneWidth, 1.0/SceneHeight);           
}

float4 PostRefract_PS(vertexOutput IN) : COLOR {
    float3 nWorldView     = normalize(IN.WorldView.xyz);
    float3 nWorldNormal   = normalize(IN.WorldNormal.xyz);
    float3 nProjPos      = (IN.ProjPos.xyz + float3(1,1,1)) * 0.5;
    
    float2 lookupOffset = GetRefractBGOffset(nWorldView, nWorldNormal);
    float3 textureColour = tex2D(SceneSampler, nProjPos.xy + lookupOffset).rgb;
 
    return float4(InvisiColour*textureColour, 1.0f);
}

float4 VapourTrail_PS(vertexOutput IN) : COLOR {
    float3 nWorldView     = normalize(IN.WorldView.xyz);
    float3 nWorldNormal   = normalize(IN.WorldNormal.xyz);
    float3 nProjPos      = (IN.ProjPos.xyz + float3(1,1,1)) * 0.5;
    
    // Use noise to modify the normal... (using Perlin's method in GPU Gems 1)
    const float epsilon = 0.00001f;
    float3 sampleCoord = IN.LocalPos.xyz + Freq*Timer*FlowDir;
    float4 texVal = tex3D(NoiseSampler, Scale * sampleCoord);   
    
    float  UnmodifiedNoiseVal = 8.0f * (abs(texVal.x - 0.25)  + abs(texVal.y - 0.125) + 
                               abs(texVal.z - 0.0625) + abs(texVal.w - 0.03125));
    //float UnmodifiedNoiseVal = texVal.x;
    float3 NoiseValDiff = (UnmodifiedNoiseVal.xxx - epsilon.xxx) / epsilon;

    nWorldNormal = normalize(nWorldNormal - NoiseValDiff);
    
    float3 refractionVec = refract(WarpAmount*nWorldView, nWorldNormal, IndexOfRefraction);
    float2 lookupOffset  = refractionVec.xy * float2(1.0/SceneWidth, 1.0/SceneHeight);
    float3 textureColour = tex2D(SceneSampler, nProjPos.xy + lookupOffset).rgb;
    float maskTexVal = tex2D(MaskSampler, IN.UV.xy).a;
    
    return float4(textureColour, UnmodifiedNoiseVal*maskTexVal);
}


///// TECHNIQUES /////////////////////////////

technique PostRefractGeom {

    pass p0 < string Script = "Draw=geometry;"; > {
        BlendEnable = true;
        BlendFunc = int2(SrcAlpha, OneMinusSrcAlpha);
        
        DepthTestEnable = true;
        DepthFunc = LEqual;
        CullFaceEnable = true;
        CullFace = Back;
        PolygonMode = int2(Front, Fill);
        
        VertexProgram = compile vp40 PostRefract_VS();        
        FragmentProgram = compile fp40 PostRefract_PS();
    }  
}

technique VapourTrail {

    pass p0 < string Script = "Draw=geometry;"; > {
        BlendEnable = true;
        BlendFunc = int2(SrcAlpha, OneMinusSrcAlpha);
        
        DepthTestEnable = true;
        DepthFunc = LEqual;
        CullFaceEnable = true;
        CullFace = Back;
        PolygonMode = int2(Front, Fill);
        
        VertexProgram = compile vp40 PostRefract_VS();        
        FragmentProgram = compile fp40 VapourTrail_PS();
    }  
}
/////////////////////////////////////// eof //
